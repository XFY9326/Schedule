plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

rootProject.ext {
    applicationId = 'tool.xfy9326.schedule'
    minSdkVersion = 23
    targetSDKVersion = 30
    buildToolsVersion = "30.0.2"
}

android {
    compileSdkVersion rootProject.ext.targetSDKVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSDKVersion
        versionCode 1
        versionName "1.0"

        resConfigs 'zh', 'en'

        packagingOptions {
            exclude 'LICENSES'
            exclude 'META-INF/*.version'
            exclude 'main/java/androidx/datastore/preferences/PreferencesProto.java'
            exclude 'DebugProbesKt.bin'
            exclude 'META-INF/CHANGES'
            exclude 'META-INF/README.md'
            exclude 'okhttp3/internal/publicsuffix/NOTICE'
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
//            zipAlignEnabled true
//            minifyEnabled true
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    ext.androidx_appcompat_version = '1.2.0'
    ext.androidx_lifecycle_version = '2.2.0'
    ext.androidx_room_version = '2.2.6'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    // AndroidX
    implementation 'androidx.core:core-ktx:1.3.2'

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"

    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.datastore:datastore-preferences:1.0.0-alpha05'

    // LifeCycle, ViewModel, LiveData
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$androidx_room_version"
    implementation "androidx.room:room-ktx:$androidx_room_version"
    kapt "androidx.room:room-compiler:$androidx_room_version"

    // Material Design
    implementation 'com.google.android.material:material:1.3.0-beta01'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    // Ktor
    implementation 'io.ktor:ktor-client-android:1.5.0'

    // Jsoup
    implementation 'org.jsoup:jsoup:1.13.1'

    // ColorPicker
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Pinyin
    implementation 'com.github.promeg:tinypinyin:2.0.3'
    implementation 'com.github.promeg:tinypinyin-lexicons-android-cncity:2.0.3'

    // iCalendar
    implementation('net.sf.biweekly:biweekly:0.6.5') {
        exclude module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core'
    }

    // Test
    testImplementation 'junit:junit:4.13.1'
    testImplementation "androidx.room:room-testing:$androidx_room_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}