plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

rootProject.ext {
    buildToolsVersion = "30.0.3"
    minSdkVersion = 23
    targetSDKVersion = 30
    applicationName = 'PureSchedule'
    applicationId = 'tool.xfy9326.schedule'
    versionCode = 9
    versionName = "1.1.7"
}

android {
    compileSdkVersion rootProject.ext.targetSDKVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSDKVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        resConfigs 'zh', 'en'
        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    outputFileName = "${rootProject.ext.applicationName}_v${versionName}_${versionCode}.apk"
                }
        }

        packagingOptions {
            exclude 'META-INF/*.version'
            exclude 'main/java/androidx/datastore/preferences/PreferencesProto.java'
            exclude 'META-INF/CHANGES'
            exclude 'META-INF/README.md'
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
//            zipAlignEnabled true
//            minifyEnabled true
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = ['ApplicationId': applicationId + applicationIdSuffix]
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = ['ApplicationId': applicationId]

            packagingOptions {
                exclude 'DebugProbesKt.bin'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    ext.androidx_appcompat_version = '1.2.0'
    ext.androidx_lifecycle_version = '2.3.0'
    ext.androidx_room_version = '2.2.6'

    ext.ktor_version = '1.5.2'

    implementation project(path: ':IO')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    // Kotlin Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization"

    // AndroidX
    implementation "androidx.core:core-ktx:$androidx_core_version"

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"

    implementation 'androidx.activity:activity-ktx:1.2.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.datastore:datastore-preferences:1.0.0-alpha08'

    // LifeCycle, ViewModel, LiveData
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$androidx_room_version"
    implementation "androidx.room:room-ktx:$androidx_room_version"
    kapt "androidx.room:room-compiler:$androidx_room_version"

    // Material Design
    implementation 'com.google.android.material:material:1.3.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // Ktor
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"

    // Jsoup
    implementation 'org.jsoup:jsoup:1.13.1'

    // ColorPicker
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.room:room-testing:$androidx_room_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}