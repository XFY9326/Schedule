plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "kotlinx-serialization"
}

def config = rootProject.ext
def androidConfig = config.android
def dependenciesVersion = config.dependencies
def kotlinxVersion = dependenciesVersion.kotlinx
def androidxVersion = dependenciesVersion.androidx

android {
    compileSdkVersion androidConfig.compileSdkVersion
    buildToolsVersion androidConfig.buildToolsVersion

    defaultConfig {
        applicationId androidConfig.applicationId
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSDKVersion
        versionCode androidConfig.versionCode
        versionName androidConfig.versionName

        resConfigs "zh", "en"
        manifestPlaceholders = ["ApplicationId": androidConfig.applicationId]
        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    outputFileName = "${config.projectName}_v${versionName}_${versionCode}_${config.gitCommitId}.apk"
                }
        }

        packagingOptions {
            exclude "META-INF/*.version"
            exclude "main/java/androidx/datastore/preferences/PreferencesProto.java"
            exclude "META-INF/CHANGES"
            exclude "META-INF/README.md"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        debug {
            storeFile file("${config.projectName}_debug.keystore")
            storePassword "debug_key"
            keyAlias "${config.projectName}"
            keyPassword "debug_key"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-$config.gitCommitId"
            applicationIdSuffix ".debug"
            manifestPlaceholders = ["ApplicationId": androidConfig.applicationId + applicationIdSuffix]
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            packagingOptions {
                exclude "DebugProbesKt.bin"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation project(path: ":IO")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinxVersion.coroutines"

    // Kotlin Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinxVersion.serialization"

    // AndroidX
    implementation "androidx.core:core-ktx:$androidxVersion.core"

    implementation "androidx.appcompat:appcompat:$androidxVersion.appcompat"
    implementation "androidx.appcompat:appcompat-resources:$androidxVersion.appcompat"

    implementation "androidx.activity:activity-ktx:1.2.1"
    implementation "androidx.fragment:fragment-ktx:1.3.1"

    implementation "androidx.constraintlayout:constraintlayout:2.0.4"

    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.datastore:datastore-preferences:1.0.0-alpha08"

    // LifeCycle, ViewModel, LiveData
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidxVersion.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxVersion.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxVersion.lifecycle"

    // Room
    implementation "androidx.room:room-runtime:$androidxVersion.room"
    implementation "androidx.room:room-ktx:$androidxVersion.room"
    kapt "androidx.room:room-compiler:$androidxVersion.room"

    // Material Design
    implementation "com.google.android.material:material:1.3.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.12.0"

    // Ktor
    implementation "io.ktor:ktor-client-android:$dependenciesVersion.ktor"
    implementation "io.ktor:ktor-client-serialization:$dependenciesVersion.ktor"

    // Jsoup
    implementation "org.jsoup:jsoup:1.13.1"

    // ColorPicker
    implementation "com.jaredrummler:colorpicker:1.1.0"

    // Test
    testImplementation "junit:junit:$dependenciesVersion.junit"
    testImplementation "androidx.room:room-testing:$androidxVersion.room"
    androidTestImplementation "androidx.test.ext:junit:$androidxVersion.test.junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxVersion.test.espresso"

    // Debug
    // debugImplementation "com.squareup.leakcanary:leakcanary-android:2.6"
}